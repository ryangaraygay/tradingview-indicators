//@version=5
indicator("MA Based Trend BarColor", shorttitle = "MA_TREND_BARCOLOR", overlay=true)

getAverageType(averageType, averageSource, averageLength) =>
	switch str.upper(averageType)
        "SMA"  => ta.sma(averageSource, averageLength)
        "EMA"  => ta.ema(averageSource, averageLength)
        "VWMA" => ta.vwma(averageSource, averageLength)
        => runtime.error("Moving average type '" + averageType + "' not found!"), na

a_type = input.string("SMA", "high timeframe", ["SMA","EMA","VWMA"], "", "1", "")
b_type = input.string("EMA", "med timeframe", ["SMA","EMA","VWMA"], "", "2", "")
c_type = input.string("EMA", "low timeframe", ["SMA","EMA","VWMA"], "", "3", "")

a_length = input.int(defval = 105, title = "length", group ="", inline = "1")
b_length = input.int(defval = 45, title = "length", group ="", inline = "2")
c_length = input.int(defval = 9, title = "length", group ="", inline = "3")

a_series = input.string("close", "source", ["open","close","hlc3"], "", "1", "")
b_series = input.string("close", "source", ["open","close","hlc3"], "", "2", "")
c_series = input.string("close", "source", ["open","close","hlc3"], "", "3", "")

getSeries(seriesName) =>
	switch str.upper(seriesName)
        "CLOSE"  => close
        "OPEN"  => open
        "HLC3"  => hlc3
        => runtime.error("Series '" + seriesName + "' not found!"), na

a = getAverageType(a_type, getSeries(a_series), a_length)
b = getAverageType(b_type, getSeries(b_series), b_length)
c = getAverageType(c_type, getSeries(c_series), c_length)

ht_color = input.color(color.red, "Color", inline = "1")
mt_color = input.color(color.orange, "Color", inline = "2")
lt_color = input.color(color.green, "Color", inline = "3")

p1 = plot(a, "slow MA", color=ht_color)
p2 = plot(b, "mid MA", color=mt_color)
p3 = plot(c, "fast MA", color=lt_color)

state_one = c > b and b > a
state_two = b > c and c > a
state_three = a > c and c > b
state_four = a > b and b > c

var color_maxup = input.color(color.new(color.green, 0), "bullish", group = "Trend-based Bar Color")
var color_up = input.color(color.new(color.blue, 0), "bullish-neutral", group = "Trend-based Bar Color")
var color_maxdown = input.color(color.new(color.red, 0), "bearish", group = "Trend-based Bar Color")
var color_down = input.color(color.new(color.orange, 0), "bearish-neutral", group = "Trend-based Bar Color")
var color_undefined = color.new(color.gray, 30)

volume_color = if state_one
    color_maxup
else if state_two
    color_up
else if state_three
    color_down
else if state_four
    color_maxdown
else
    color_undefined

bodyColor = close > open ? na : volume_color
plotcandle(open, high, low, close, color=bodyColor, wickcolor = volume_color, bordercolor = volume_color)

alertcondition(barstate.isconfirmed and volume_color != volume_color[1], "MA-based Trend Changed Alert", "Moving Average based Trend has changed.")